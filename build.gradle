buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:1.5.0'
    }
}

plugins {
	id "org.sonarqube" version "1.0"
}

description 'Web application launcher for HBM devices.'

version = '0.9.0'

ext {
	isReleaseBuild = false
}

String getGitHash() {
    return 'git rev-parse --verify HEAD'.execute().text.trim()
}

def getTotalCommits() {
  return 'git rev-list HEAD --count'.execute().text.trim()
}

def isDirty() {
	def dirty = 'git diff --shortstat'.execute().text.trim()
	return dirty != ""
}

def isRelease() {
	def getRelease = "git describe --exact-match HEAD".execute()
	getRelease.waitFor()
	return getRelease.exitValue() == 0
}
//set build variables based on build type (release, continuous integration, development)

ext.isReleaseBuild = isRelease()
def totalCommits = getTotalCommits()

if (!ext.isReleaseBuild) {
	version = version + "-" + totalCommits
}

if (isDirty()) {
	version = version + "+dirty"
}

repositories {
	mavenCentral()
}
	
apply plugin: 'com.android.application'

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
        }
    }
}

dependencies {
	compile 'com.google.code.gson:gson:2.6.2'
	compile 'com.hbm:devscan:1.6.0'
	compile 'com.android.support:appcompat-v7:23.2.1'
	compile 'com.android.support:cardview-v7:23.2.1'
	compile 'com.android.support:design:23.2.1'
	compile 'com.android.support:recyclerview-v7:23.2.1'
	compile 'com.squareup.picasso:picasso:2.5.2'
    testCompile 'junit:junit:4.12'
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.hbm.devices.scan.ui.android"
        minSdkVersion 17
        targetSdkVersion 23
        versionCode totalCommits.toInteger()
        versionName version
		buildConfigField("long", "TIMESTAMP", System.currentTimeMillis() + "L")
		buildConfigField("String", "GITHASH", "\"${getGitHash()}\"")
    }

	signingConfigs {
		release {
			//storeFile file("myReleaseKey.keystore")
			//storePassword "passwd"
			//keyAlias "MyReleaseKey"
			//keyPassword "passwd"
		}
	}

	buildTypes {
		release {
			minifyEnabled false
			//signingConfig signingConfigs.release
		}
	}

	lintOptions {
		checkAllWarnings true
    }
}

sonarqube {
	properties {
		def android_sdk = System.getenv("ANDROID_SDK")

		property "sonar.projectName", "Android Scan"
		property "sonar.projectVersion", "1.0"
		property "sonar.projectDescription", "Android scanner for HBM devices"
		property "sonar.projectKey", "my.group:project-key"
		property "sonar.host.url", "http://localhost:9000"
		property "sonar.sourceEncoding", "UTF-8"
		property "sonar.sources", "src"
		property "sonar.java.binaries", "build/intermediates/classes/release/"
		property "sonar.java.libraries", "libs/*.jar,${android_sdk}/platforms/android-23/android.jar,${android_sdk}/extras/android/support/v4/android-support-v4.jar"
	}
}

tasks.sonarqube.dependsOn = ["build"]

task createWrapper(type: Wrapper) {
	gradleVersion = '2.10'
}

