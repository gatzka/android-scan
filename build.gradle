buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.0'
        classpath 'com.google.gms:google-services:3.1.1'
    }
}

plugins {
    id "org.sonarqube" version "2.5"
}

description 'Web application launcher for HBM devices.'

version = '1.15.0'

ext {
    isReleaseBuild = false
}

static String getGitHash() {
    return 'git rev-parse --verify HEAD'.execute().text.trim()
}

static def getTotalCommits() {
    return 'git rev-list HEAD --count'.execute().text.trim()
}

static def isDirty() {
    def dirty = 'git diff --shortstat'.execute().text.trim()
    return dirty != ""
}

static def isRelease() {
    def getRelease = "git describe --exact-match --tags HEAD".execute()
    getRelease.waitFor()
    return getRelease.exitValue() == 0
}
//set build variables based on build type (release, continuous integration, development)

ext.isReleaseBuild = isRelease()
def totalCommits = getTotalCommits()

if (!ext.isReleaseBuild) {
    version = version + "-" + totalCommits
}

if (isDirty()) {
    version = version + "+dirty"
}

repositories {
    mavenCentral()
}

apply plugin: 'com.android.application'

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
        }
    }
}

dependencies {
    compile 'com.google.code.gson:gson:2.8.2'
    compile 'com.hbm:devscan:2.0.0'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'de.psdev.licensesdialog:licensesdialog:1.8.3'
    compile 'com.google.firebase:firebase-crash:10.2.1'
    testCompile 'junit:junit:4.12'
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.hbm.devices.scan.ui.android"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode totalCommits.toInteger()
        versionName version
        buildConfigField("long", "TIMESTAMP", System.currentTimeMillis() + "L")
        buildConfigField("String", "GITHASH", "\"${getGitHash()}\"")
    }

    signingConfigs {
        release {
            //storeFile file(RELEASE_STORE_FILE)
            //storePassword RELEASE_STORE_PASSWORD
            //keyAlias RELEASE_KEY_ALIAS
            //keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            shrinkResources false
            debuggable false
            zipAlignEnabled true
            //		signingConfig signingConfigs.release
        }
    }

    lintOptions {
        checkAllWarnings true
    }
}

sonarqube {
    properties {
        def android_sdk = System.getenv("ANDROID_SDK")

        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.sources", "src"
        property "sonar.java.binaries", "build/intermediates/classes/release/"
        property "sonar.java.libraries", "${android_sdk}/platforms/android-25/android.jar,${android_sdk}/extras/android/support/v4/android-support-v4.jar"
    }
}

tasks.sonarqube.dependsOn = ["build"]

task createWrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}


apply plugin: 'com.google.gms.google-services'
