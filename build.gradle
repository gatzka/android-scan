buildscript {
    repositories {
        jcenter()
		google()
        maven {
            url 'https://maven.fabric.io/public'
        }
		maven { url 'https://maven.google.com' }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
        classpath 'com.google.firebase:firebase-plugins:1.1.5'
        classpath 'io.fabric.tools:gradle:1.25.2'
        classpath 'com.google.gms:google-services:3.3.1'
    }
}

plugins {
    id "org.sonarqube" version "2.6.2"
}

description 'Web application launcher for HBM devices.'

version = '1.18.0'

ext {
    isReleaseBuild = false
}

static String getGitHash() {
    return 'git rev-parse --verify HEAD'.execute().text.trim()
}

static def getTotalCommits() {
    return 'git rev-list HEAD --count'.execute().text.trim()
}

static def isDirty() {
    def dirty = 'git diff --shortstat'.execute().text.trim()
    return dirty != ""
}

static def isRelease() {
    def getRelease = "git describe --exact-match --tags HEAD".execute()
    getRelease.waitFor()
    return getRelease.exitValue() == 0
}
//set build variables based on build type (release, continuous integration, development)

ext.isReleaseBuild = isRelease()
def totalCommits = getTotalCommits()

if (!ext.isReleaseBuild) {
    version = version + "-" + totalCommits
}

if (isDirty()) {
    version = version + "+dirty"
}

apply plugin: 'com.android.application'

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
        }
    }

    repositories {
        mavenCentral()
        google()
        jcenter()
        maven {
            url 'https://maven.google.com/'
        }

    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.hbm:devscan:2.1.0'
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:preference-v7:27.1.1'
    implementation 'com.android.support:cardview-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:recyclerview-v7:27.1.1'
    implementation 'com.android.support:exifinterface:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'
	implementation 'com.google.firebase:firebase-core:16.0.3'
    implementation 'com.google.firebase:firebase-crash:16.2.0'
    implementation 'com.google.firebase:firebase-perf:16.1.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.5'
    testImplementation 'junit:junit:4.12'
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'

    defaultConfig {
        applicationId "com.hbm.devices.scan.ui.android"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode totalCommits.toInteger()
        versionName version
        buildConfigField("long", "TIMESTAMP", System.currentTimeMillis() + "L")
        buildConfigField("String", "GITHASH", "\"${getGitHash()}\"")
    }

    signingConfigs {
        release {
            //storeFile file(RELEASE_STORE_FILE)
            //storePassword RELEASE_STORE_PASSWORD
            //keyAlias RELEASE_KEY_ALIAS
            //keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            shrinkResources false
            debuggable false
            zipAlignEnabled true
            //		signingConfig signingConfigs.release
        }
    }

    lintOptions {
        checkAllWarnings true
    }
}

sonarqube {
    properties {
        def android_sdk = System.getenv("ANDROID_SDK")

        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.sources", "src"
        property "sonar.java.binaries", "build/intermediates/classes/release/"
        property "sonar.java.libraries", "${android_sdk}/platforms/android-25/android.jar,${android_sdk}/extras/android/support/v4/android-support-v4.jar"
    }
}

tasks.sonarqube.dependsOn = ["build"]

task createWrapper(type: Wrapper) {
    gradleVersion = '4.8.1'
}

apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'io.fabric'
